% Local economy


!variables

    "Public infrastructure auxiliary variable" AA
    "Leontief parameter on import inputs in local sector" GAMMA_Md`x
    "Leontief parameter on D3 inputs in local sector" GAMMA_D3`x
    "Monetary policy reaction term" React`x

    "Total household consumption" C

    "Shadow price of OPT household budget" LAMBDA
    "Consumption of OPT households" Copt
    "Reference consumption to consumption of OPT households" Cref_Copt`x
    "Private residential investment to GDP" PiIh_NGDP`x

    "Consumption of Hand-to-Mouth Households" Chtm
    "Auxiliary Variable for Consumption to Net Worth Ratio" Copt_Vopt_nu0`x
    "Disposable Labor Income of Optimizing Households" DLIopt
    "Household real net worth" Vopt
    "Disposable labor income of HTM households" DLIhtm
    "Hours worked in local sector" Nd
    "Wage bill in local sector to GDP" WNd_NGDP`x
    "Physical investment in local sector" Id

    "Profits in local sector to GDP" PIEd_NGDP`x
    "Profits in local sector to GDP" PIEd1_NGDP`x
    "Profits in local sector to GDP" PIEd2_NGDP`x
    "Profits in local sector to GDP" PIEd3_NGDP`x

    "Private residential and overhead capital" Kh
    "Private residential and overhead investment" Ih

    "Variable hours worked in local sector" NNd
    "Overhead hours worked in local sector" N0d
    "Imported inputs to local sector" Md
    "Production capital in local sector" Kd
    "Local production of intermediate goods" D
    "Overall technology" A
    "Relative productivity in local sector" Ad
    "Total productivity in local sector" AAd
    "Capital in nonprimary export sector" Kz
    "Investment in nonprimary export sector" Iz

    "Household net worth to GDP" PcVopt_NGDP`x
    "Capital in local sector to GDP" PkdKd_NGDP`x

    "Capital in nonprimary exports to GDP" PkzKz_NGDP`x

    "Stage T-1 output in local sector" D1
    "Stage T-2 output in local sector" D2
    "Stage T-3 output in local sector" D3
    "Marginal cost of stage T-1 output in local sector" Pd1
    "Marginal cost of stage T-2 output in local sector" Pd2
    "Marginal cost of stage T-3 output in local sector" Pd3
    "Marked-up marginal cost in local sector" Pd
    "Price of investment" Pi
    "Consumer goods prices" Pc
    "Consumer goods prices excl VAT" Pcx
    "Price of capital in local sector" Pkd
    "Price of capital in nonprimary export sector" Pkz
    "Rental price of capital in local production" Prkd
    "Rental price of capital in nonprimary exports" Prkz

    "Price of import for local sector" Pmd
    "Nominal exchange rate" S
    "Nominal exchange rate, Y/Y" dS

    "Government local-currency debt rate" Rg
    "Government foreign-currency debt rate" Rg_star
    "Household local-currency interest rate" Rh
    "Premium on government debt" PREMg
    "Premium on household debt" PREMh

    "Price of consumption, Y/Y" dPc
    "Price of consumption excl VAT, Y/Y" dPcx
    "Price of investment, Y/Y" dPi
    "Nominal GDP, Y/Y" dNGDP

    "Private investment in local production to GDP" PiId_NGDP`x
    "Investment to capital in local production" PiId_PkdKd`x
    "Household NFA to GDP"  Bhb_NGDP`x
    "Auxiliary variable for disposable labor income to consumption" DLIopt_Copt`x

    PmdMd_NGDP`x


!log-variables !all-but

    !list(`x)


!shocks

    "Shock to overall productivity" shk_A
    "Shock to relative sector productivity in local sector" shk_Ad
    "Shock to exchange rate parity" shk_S
    shk_GAMMA_Md
    shk_GAMMA_D3


!parameters

    "Overhead labor to labor in local sector" ss_N0d_Nd

    "Share of remmittances received by HTM households" omega

    "Efficiency of public infrastructure" iota_kg
    "Efficiency of government support of local supply side" iota_tf

    "Monetary policy reaction to inflation" kappa_dPc
    "Monetary policy reaction to depreciation" kappa_dS
    "A/R Monetary policy rate" rho_Rg
    "Imperfections in forex arbitrage" zeta_S

    "A/R Leontief share coefficients" rho_GAMMA
    "Calibration constant for Leontief shares in local production" Ad2

    psi_dg

    % Steady-state parameters
    "Discount factor" beta
    "Current income sensitivity of intertemporally OPT households" chi_DLI
    "Sensitivity of utility to net worth" nu
    "Intercept in net worth utility component" nu0
    "Depreciation of production capital in local sector" delta_Kd
    "Depreciation of residential capital" delta_Kh
    "Depreciation of capital in nonprimary export sector" delta_Kz
    "Markup in final goods sector" mu_CI
    "Markup over variable factors in local sector" mu_D3
    "Import Share of local sector" gamma_Md
    "Labor Share of local sector" gamma_Nd

    "Intercept in government spread" zeta_Rg0
    "Sensitivity of government spread to debt" zeta_Rg1
    "Intercept in household spread" zeta_Rh0
    "Sensitivity of household spread to debt" zeta_Rh1
    "Share of primary inputs used in local sector" gamma_J;

    % Directly calibrated ratios and rates
    %"S/S Remittances from HTM households to HTM wage bill" ss_TFwh_WNhtm
    "S/S Remittances from HTM households to HTM wage bill" ss_TFwh_NGDP
    "S/S Private Residential (Housing) Investment to Investment in Local Production Ratio" ss_Kh_Kd
    "S/S Rate of Cange in Overall Productivity" ss_dA
    "Inflation Target" ss_dPc
    "S/S Relative Sectoral Productivity in Local Production" ss_Ad
    %"S/S Overhead Labor in Local Production" ss_N0d

    % Transitory Parameters
    "Habit in consumption" chi_C
    "Investment adjustment cost in local sector" xi_Id
    "Investment adjustment cost in nonprimary export sector" xi_Iz
    "Consumer Price Adjustment Cost" xi_Pc
    "Investment price adjustment cost" xi_Pi
    %"Cost of hiring-firing variable labor in local sector" xi_NNd
    "Autoregression in overall productivity" rho_A
    "Autoregression in relative sectoral productivity in local production" rho_Ad
    "Residential (housing) investment reaction to private investment to GDP" lambda_Ih1


!substitutions

    % Investment adjustment cost term in local production
    AdjId0 := xi_Id*[log(Id) - log(Id{-1}) - log(ss_dA) ];
    AdjId1 := xi_Id*[log(Id{+1}) - log(Id) - log(ss_dA) ];

    % Investment adjustment cost term in nonprimary export sector
    AdjIz0 := xi_Iz*[ log(Iz)-log(Iz{-1}) - log(ss_dA) ];
    AdjIz1 := xi_Iz*[ log(Iz{+1})-log(Iz) - log(ss_dA) ];

    % Consumer price adjustment cost term
    AdjPc0 := xi_Pc*[ log(dPcx) - log(dPcx{-1}) ];
    AdjPc1 := xi_Pc*[ log(dPcx{+1}) - log(dPcx) ];

    % Investment price adjustment cost term
    AdjPi0 := xi_Pi*[ log(dPi) - log(dPi{-1}) ];
    AdjPi1 := xi_Pi*[ log(dPi{+1}) - log(dPi) ];

    % Current net worth effect
    NetWorthEff = ((Copt/Vopt - nu0)/(Pc*Copt));

    % Scale constant for production function in local sector
    gamma_D2 := (gamma_Md^gamma_Md * (1-gamma_Md)^(1-gamma_Md));


!equations

% __Optimizing households__

    "Intertemporal choice by OPT households"
    LAMBDA = ...
        + beta*LAMBDA{+1}*Rh ...
        + nu*$NetWorthEff$;

    "Demand for consumption by OPT households"
    LAMBDA*Pc*(Copt - Cref_Copt*Copt) = (1 - &Cref_Copt) ...
    !! LAMBDA*Pc*Copt = 1;

    "Reference consumption of OPT households"
    Cref_Copt*Copt = chi_DLI*DLIopt + chi_C*ss_dA*Copt{-1} ...
    !! Cref_Copt = chi_DLI*DLIopt/Copt + chi_C;

    "Net worth of OPT households"
    Pc*Vopt = (Pkd*(Kd + Kh) + Pkz*Kz + Bhb_NGDP*NGDP)/(1-omega);
    % Pc*Vopt = (Pkd*(Kd + Kh)) / (1 - omega);

    "Real current income of OPT households"
    Pc*DLIopt = (WNopt_NGDP*(1 - TRlit) + TFgopt_NGDP)*NGDP;

    "Calibration of net worth utility component"
    Copt_Vopt_nu0 = Copt/Vopt - nu0;


    % Supply of capital to local sector

    "Demand for investment in local sector"
    Pkd = Pi*(1 + $AdjId0$ - beta*$AdjId1$ - iota_tf*TRgd) ...
    !! Pkd = Pi*(1 - iota_tf*TRgd);

    "Price of capital in local sector"
    LAMBDA*Pkd = ...
        + beta*LAMBDA{+1}*[Pkd{+1}*(1-delta_Kd) + Prkd{+1}] ...
        + nu*$NetWorthEff$*Pkd ...
    ;

    "Stock-flow capital-investment for local sector"
    Kd = (1 - delta_Kd) * Kd{-1} + Id;


    % Supply of capital to nonprimary export sector

    "Stock-flow capital in nonprimary export sector"
    Kz = (1 - delta_Kz) * Kz{-1} + Iz;

    "Replacement cost of capital in nonprimary export sector"
    Pkz = Pi*(1 + $AdjIz0$ - beta*$AdjIz1$ - iota_tf*TRgz) ...
    !! Pkz = Pi*(1 - iota_tf*TRgz);

    "Price of capital in nonprimary export sector"
    LAMBDA*Pkz = ...
        + beta*LAMBDA{+1}*[Pkz{+1}*(1-delta_Kz) + Prkz{+1}] ...
        + nu*$NetWorthEff$*Pkz ...
    ;


% __Hand-to-mouth households__

    "Real current income of HTM households"
    Pc*DLIhtm/NGDP = (WNhtm_NGDP*(1 - TRlit) + TFghtm_NGDP + TFwhtm_NGDP);

    "Demand for consumption by OPT households"
    Chtm = DLIhtm;


% __Total private consumption__

    "Total private consumption"
    C = omega*Chtm + (1 - omega)*Copt;


% __Residential and overhead investment__

    "Residential and overhead investment"
    log(Ih) = log(ss_dA*Ih{-1}) + lambda_Ih1*log(Kd{+1}*ss_Kh_Kd/Kh{+1}) ...
    !! Kh = ss_Kh_Kd * Kd;

    "Residential and overhead capital"
    Kh = (1-delta_Kh)*Kh{-1} + Ih;


% __Local distribution of consumer and investment goods__

    % Staget T-0 in local sector: Distribution
    D = D1;
    Pd = mu_CI * Pd1;

    % Final consumer price setting
    Pd = Pcx*[1 + (mu_CI-1)*($AdjPc0$ - beta*$AdjPc1$)] ...
    !! Pcx = Pd;

    % VAT applied on consumer goods
    Pc = Pcx * (1 + TRvat);

    % Final investment price setting
    Pd = Pi*[1 + (mu_CI-1)*($AdjPi0$ - beta*$AdjPi1$)] ...
    !! Pi = Pd;


% __Production for local markets__


    % Stage T-3 in local sector: Domestic inputs

    "Breakdown of total labor into overhead and variable in local sector"
    Nd = N0d + NNd;

    "Overhead labor in local sector"
    N0d = ss_N0d_Nd * &Nd;

    "Wage bill in local sector"
    WNd_NGDP = W*Nd / NGDP;

    "Stage T-3 production technology in local sector"
    D3 = Kd{-1}^(1 - gamma_Nd) * (A * Ad * NNd)^gamma_Nd;

    "Demand for labor in local sector"
    gamma_Nd * Pd3 * D3 = mu_D3 * W * NNd;

    "Demand for capital in local sector"
    (1 - gamma_Nd) * Pd3 * D3 = mu_D3 * Prkd * Kd{-1};


    % Stage T-2 in local sector: Nonprimary import

    "Stage T-2 production technology in local sector"
    Pd2 = GAMMA_Md*Pmd + GAMMA_D3*Pd3 ...
    !! D2 = Ad2 * (1/$gamma_D2$) * Md^gamma_Md * D3^(1-gamma_Md);

    "Demand for nonprimary imports in local sector"
    Md = GAMMA_Md * D2 ...
    !! gamma_Md * Pd2 * D2 = Pmd * Md;

    "Demand for local input in local sector"
    D3 = GAMMA_D3 * D2 ...
    !! (1 - gamma_Md) * Pd2 * D2 = Pd3 * D3;

    "Leontief coefficient on import input in T-2 output in local sector"
    GAMMA_Md = ...
        + rho_GAMMA * (GAMMA_Md{-1}) ...
        + (1 - rho_GAMMA) * (&Md / &D2) ...
        + shk_GAMMA_Md ...
    !! GAMMA_Md = &Md / &D2;

    "Leontief coefficient on D3 in T-2 output in local sector"
    GAMMA_D3 = ...
        + rho_GAMMA * (GAMMA_D3{-1}) ...
        + (1 - rho_GAMMA) * (&D3 / &D2) ...
        + shk_GAMMA_D3 ...
    !! GAMMA_D3 = &D3 / &D2;


    % Stage T-1 in local sector: Energy inputs

    D1 = D2 / (1 - gamma_J);

    D1 = J / gamma_J;

    Pd1 = (1 - gamma_J)*Pd2 + gamma_J*Pj;


% __Productivity (Technology)__

    "Overall productivity"
    log(A/A{-1}) = ...
        + rho_A * log(A{-1}/A{-2}) ...
        + (1 - rho_A) * (log(ss_dA) + shk_A) ...
    !! A = ss_dA*A{-1};

    "Public infrastructure augmented productivity"
    AA = A + iota_kg*Kg;

    "Relative productivity in local sector"
    log(Ad) = rho_Ad*log(Ad{-1}) + (1-rho_Ad)*(log(ss_Ad) + shk_Ad) ...
    !! Ad = ss_Ad;

    "Total productivity in local sector"
    AAd = AA * Ad ...
    !! AAd = AA * ss_Ad;


% __Monetary policy__

    "Monetary policy reaction function"
    log(Rg) = ...
        + rho_Rg * log(Rg{-1}) ...
        + (1 - rho_Rg) * (log(&Rg) + React) ...
    !! dPc = ss_dPc;


    "Monetary policy reaction term"
    React = ...
        + kappa_dPc * log(dPc{+1}/ss_dPc) ...
        + kappa_dS * log(dS{+1}/&dS) ...
    !! React = 0;


% __World prices in local currency__

    "Price of import for local sector"
    Pmd = S * Pw_star;


% __Interest rate structure__

    "Premium on country and government rate"
    PREMg = exp(zeta_Rg0 - zeta_Rg1*NIP_NGDP);

    "Country and government foreign-currency debt rate"
    Rg_star = Rw_star * PREMg;

    "Parity condition between local and foreign currency debt rates"
    Rg/Rg_star = (S{+1}/S) * (dS/&dS)^(-zeta_S) ...
    !! Rg = Rg_star * dS;

    "Premium on household debt rate"
    PREMh = exp(zeta_Rh0 + zeta_Rh1*Bhb_NGDP);

    "Household debt rate"
    Rh = Rg * PREMh;


% __Growth rates__

    dPc = Pc/Pc{-1};
    dPcx = Pcx/Pcx{-1};
    dPi = Pi/Pi{-1};
    dNGDP = NGDP/NGDP{-1};
    dS = S/S{-1};


% __Reporting and calibration ratios__

    PiId_NGDP*NGDP = Pi*Id;
    PiId_PkdKd = Pi*Id / (Pkd*Kd);
    DLIopt_Copt = DLIopt / Copt;
    PcVopt_NGDP*NGDP = Pc*Vopt;
    PkdKd_NGDP*NGDP = Pkd*Kd;
    PkzKz_NGDP = Pkz*Kz / NGDP;
    PmdMd_NGDP*NGDP = Pmd*Md;

    "Profits of local producers to GDP"
    PIEd3_NGDP = (Pd3*D3 - W*Nd - Prkd*Kd{-1}) / NGDP;
    PIEd2_NGDP = (Pd2*D2 - Pd3*D3 - Pmd*Md) / NGDP;
    PIEd1_NGDP = (Pd1*D1 - Pd2*D2 - Pj*J) / NGDP;
    PIEd_NGDP  = (Pcx*(C+Gg) + Pi*I - W*Nd - Prkd*Kd{-1} - Pmd*Md - Pj*J) / NGDP;

    "Private residential investment to GDP"
    PiIh_NGDP = Pi*Ih / NGDP;


