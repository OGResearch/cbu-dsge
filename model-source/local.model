% Local economy


!variables

    "Public infrastructure auxiliary variable" AA
    D1, D2, D3, Pd3
    Md_D2, D3_D2
    "Monetary policy reaction term" React

    "Shadow Price of Optimizing Household Budget" Lambda
    "Total Private Consumption" Ch
    "Consumption of Optimizing Households" Copt
    "Private residential investment to GDP" PiIh_NGDP

    "Consumption of Hand-to-Mouth Households" Chtm
    "Auxiliary Variable for Consumption to Net Worth Ratio" Copt_Vh_nu0
    "Disposable Labor Income of Optimizing Households" DLIopt
    % "Total disposable labor income to GDP" PcDLI_NGDP
    "Household real net worth" Vh
    "Disposable labor income of hand-to-mouth households" DLIhtm
    "Hours worked in local sectors" Nd
    "Wage bill in local sectors to GDP" WNd_NGDP
    "Physical investment in local sectors" Id

    "Profits in local sectors to GDP" PIEd_NGDP

    "Private residential and overhead capital" Kh
    "Private residential and overhead investment" Ih

    "Variable hours worked in local sectors" NNd
    "Overhead hours worked in local sectors" N0d
    "Imported inputs to local sectors" Md
    "Production capital in local sectors" Kd
    "Local production of intermediate goods" D
    "Overall technology" A
    "Relative productivity in local sectors" Ad
    "Total productivity in local sectors" AAd


    "Household net worth to GDP" PcVh_NGDP
    "Capital in local sectors to GDP" PkdKd_NGDP

    "Capital in nonprimary exports to GDP" PkzKz_NGDP

    "Marginal cost of local intermediate goods" Pd1
    "Markuped marginal cost in local sectors" Pd
    "Price of Local Intermediate Goods without Commodity costs" Pd2
    "Price of investment" Pi
    "Consumer Goods Prices" Pc
    "Consumer Goods Prices w/o VAT" Pcx
    "Price of capital in local sector" Pkd
    "Price of capital in nonprimary export sector" Pkz
    "Rental Price of Capital in Local Production" Prkd

    "Price of imported inputs in local sectors" Pmd
    "World prices in local currency" Pw
    "Nominal exchange rate" S
    "Nominal exchange rate, Y/Y" dS

    "Government local-currency debt rate" Rg
    "Government foreign-currency debt rate" Rg_star
    "Household shadow interest rate" Rh
    "Government-world spread" Rg_Rw

    "Gross Rate of Change in Consumer Prices" dPc
    "Gross Rate of Change in Consumer Prices w/o VAT" dPcx
    "Gross Rate of Change in Investment Prices" dPi
    "Gross Rate of Change in Nominal GDP" dNGDP

    "Private Investment in Local Production to GDP Ratio" PiId_NGDP
    "Investment to Capital Ratio in Local Production" PiId_PkdKd
    "Household NFA to GDP Ratio"  Bhw_NGDP
    "Auxiliary Variable for Disposable Labor Income to Consumption Ratio" DLIopt_Copt

    PmdMd_NGDP


!log-variables !all-but

    PiIh_NGDP
    Copt_Vh_nu0
    PiId_NGDP
    PiId_PkdKd
    Bhw_NGDP
    PcVh_NGDP
    PkdKd_NGDP
    PkzKz_NGDP
    DLIopt_Copt
    Md_D2
    D3_D2
    PIEd_NGDP
    %N0d_Nd
    React
    PmdMd_NGDP
    WNd_NGDP


!shocks

    "Shock to overall productivity" shk_A
    "Shock to relative sector productivity in local sectors" shk_Ad
    "Shock to exchange rate parity" shk_S
    shk_Md_D2
    shk_D3_D2


!parameters

    "Overhead labor to labor in local sector" ss_N0d_Nd

    "Share of remmittances received by HTM househodls" omega_TFwh

    "Efficiency of public infrastructure" iota_kg
    "Efficiency of government support of local supply side" iota_tf

    "Monetary policy reaction to inflation" kappa_dPc
    "Monetary policy reaction to depreciation" kappa_dS
    "A/R Monetary policy rate" rho_Rg
    "Imperfections in forex arbitrage" lambda_S

    rho_Md_D2
    rho_D3_D2
    psi_dg

    % Steady-state parameters
    "Discount Factor" beta
    "Current Income Sensitivity of Intertemporally Optimizing Households" chi_dli
    "Sensitivity of utility to net worth" nu
    "Intercept in net worth utility component" nu0
    "Depreciation of production capital" delta_Kd
    "Depreciation of residential capital" delta_Kh
    "Markup in final goods sectors" mu_CI
    "Markup over variable factors in local sectors" mu_D2
    "Import Share of local sectors" gamma_Md
    "Labor Share of local sectors" gamma_Nd

    "Intercept in government spread" zeta_Rg0
    "Sensitivity of government spread to debt" zeta_Rg1
    "Intercept in household spread" zeta_Rh0
    "Sensitivity of household spread to debt" zeta_Rh1
    "Share of primary inputs used in local sectors" gamma_J;

    % Directly calibrated ratios and rates
    %"S/S Remittances from HTM households to HTM wage bill" ss_TFwh_WNhtm
    "S/S Remittances from HTM households to HTM wage bill" ss_TFwh_NGDP
    "S/S Private Residential (Housing) Investment to Investment in Local Production Ratio" ss_Kh_Kd
    "S/S Rate of Change in Overall Productivity" ss_dA
    "Inflation Target" ss_dPc
    "S/S Relative Sectoral Productivity in Local Production" ss_Ad
    %"S/S Overhead Labor in Local Production" ss_N0d

    % Transitory Parameters
    "Habit in consumption" chi_c
    "Capital versus investment adjustment cost" phi_Y
    "Intensity of capital or investment adjustment cost" xi_Id
    "Consumer Price Adjustment Cost" xi_Pc
    "Investment price adjustment cost" xi_Pi
    "Cost of hiring-firing variable labor in local sectors" xi_NNd
    "Autoregression in overall productivity" rho_A
    "Autoregression in relative sectoral productivity in local production" rho_Ad
    "Residential (housing) investment reaction to private investment to GDP" lambda_Ih1


!substitutions

    % Auxiliary Constant to Make MUC equal 1/C in steady state
    chi_DLI0 := (1-chi_dli*&DLIopt_Copt-chi_c);

    % Investment adjustment cost term in local production
    xi_Id0 := ((1-phi_Y)*log(ss_dA - (1-delta_Kd)) + phi_Y*log(ss_dA));
    AdjId0 := xi_Id*[ log(Id)-(1-phi_Y)*log(Kd{-1})-phi_Y*log(Id{-1}) - $xi_Id0$ ];
    AdjId1 := xi_Id*[ log(Id{+1})-(1-phi_Y)*log(Kd)-phi_Y*log(Id) - $xi_Id0$ ];

    % Consumer price adjustment cost term
    AdjPc0 := xi_Pc*[ log(dPcx) - log(dPcx{-1}) ];
    AdjPc1 := xi_Pc*[ log(dPcx{+1}) - log(dPcx) ];

    % Investment price adjustment cost term
    AdjPi0 := xi_Pi*[ log(dPi) - log(dPi{-1}) ];
    AdjPi1 := xi_Pi*[ log(dPi{+1}) - log(dPi) ];

    AdjNNd := xi_NNd*[ log(NNd/NNd{-1}) - beta*log(NNd{+1}/NNd) ];

    gamma_D2 := (gamma_Md^gamma_Md * (1-gamma_Md)^(1-gamma_Md));


!equations

% __Optimizing households__

    "Intertemporal choice by optimizing households"
    Lambda = ...
        + beta*Lambda{+1}*Rh ...
        + nu*(Copt/Vh - nu0)/(Pc*Copt);

    "Demand for consumption by optimizing households"
    Lambda*Pc*(Copt - chi_dli*DLIopt - chi_c*ss_dA*Copt{-1}) = 1;

    "Net worth of optimizing households"
    Pc*Vh = Pkd*(Kd + Kh) + (Bgh_NGDP - Bhw_NGDP)*NGDP;

    "Real current income of optimizing households"
    Pc*DLIopt = (WNopt_NGDP + TFgopt_NGDP)*NGDP;

    "Calibration of net worth utility component"
    Copt_Vh_nu0 = Copt/Vh - nu0;


    % Supply of capital to nonprimary export sector

    "Price of production capital for local sectors"
    Pkd = Pi*(1 + $AdjId0$ - beta*phi_Y*$AdjId1$ - iota_tf*TRgd) ...
    !! Pkd = Pi*(1 - iota_tf*TRgd);

    "Creation of production capital for local sectors"
    Lambda*Pkd = ...
        + beta*Lambda{+1}*[ ...
            Pkd{+1}*(1-delta_Kd) + Prkd{+1} + Pkd{+1}*(1-phi_Y)*&PiId_PkdKd*ss_dA*$AdjId1$ ...
        ] ...
        + nu*(Copt/Vh - nu0)*Pkd/(Pc*Copt) ...
    !! Lambda*Pkd = ...
        + beta*Lambda{+1}*[ ...
            Pkd{+1}*(1-delta_Kd) + Prkd{+1} ...
        ] ...
        + nu*(Copt/Vh - nu0)*Pkd/(Pc*Copt) ...
    ;

    "Stock-flow capital-investment for local sectors"
    Kd = (1 - delta_Kd) * Kd{-1} + Id;


    % Supply of capital to nonprimary export sector

    "Stock-flow capital in nonprimary export sector"
    Kz = (1 - delta_Kz) * Kz{-1} + Iz;

    "Replacement cost of capital in nonprimary export sector"
    Pkz = Pi;

% __Hand-to-mouth households__

    "Real current income of hand-to-mouth households"
    Pc*DLIhtm = (WNhtm_NGDP + TFghtm_NGDP - omega_TFwh*TFwh_NGDP)*NGDP;

    "Demand for consumption by optimizing households"
    Chtm = DLIhtm;


% __Total private consumption__

    "Total private consumption"
    Ch = Copt + Chtm;


% __Residential and overhead investment__

    "Residential and overhead investment"
    log(Ih) = log(ss_dA*Ih{-1}) + lambda_Ih1*log(Kd{+1}*ss_Kh_Kd/Kh{+1}) ...
    !! Kh = ss_Kh_Kd * Kd;

    "Residential and overhead capital"
    Kh = (1-delta_Kh)*Kh{-1} + Ih;


% __Local Distribution of Consumer and Investment Goods__

    % Staget T-0 in local sectors: Distribution
    D = D1;
    Pd = mu_CI*Pd1;

    % Final consumer price setting
    Pd = Pcx*[1 + (mu_CI-1)*($AdjPc0$ - beta*$AdjPc1$)] ...
    !! Pcx = Pd;

    % VAT applied on consumer goods
    Pc = Pcx * (1 + TRvat);

    % Final investment price setting
    Pd = Pi*[1 + (mu_CI-1)*($AdjPi0$ - beta*$AdjPi1$)] ...
    !! Pi = Pd;


% __Production for local markets__


    % Stage T-3 in local sectors: Domestic inputs

    "Breakdown of total labor into overhead and variable in local sectors"
    Nd = N0d + NNd;

    "Overhead labor in local sectors"
    N0d = ss_N0d_Nd * &Nd;

    "Wage bill in local sectors"
    WNd_NGDP = Wd*Nd / NGDP;

    "Stage T-3 production technology in local sectors"
    D3 = Kd{-1}^(1-gamma_Nd) * (A*Ad*NNd)^gamma_Nd;

    "Demand for labor in local sectors"
    gamma_Nd * Pd3*D3 = Wd * NNd * (1 + $AdjNNd$) ...
    !! gamma_Nd * Pd3*D3 = Wd * NNd;

    "Demand for capital in local sectors"
    (1-gamma_Nd)*Pd3*D3 = Prkd*Kd{-1};


    % Stage T-2 in local sectors: Nonprimary import

    "Stage T-2 production technology in local sectors"
    Pd2*D2 = mu_D2*(Pmd*Md + Pd3*D3) ...
    !if LongCobbDouglas
        !! D2 = (1/$gamma_D2$) * Md^gamma_Md * D3^(1-gamma_Md) ...
    !else
        !! Pd2*D2 = mu_D2*(Pmd*Md + Pd3*D3) ...
    !end
    ;

    "Demand for nonprimary imports in local sectors"
    Md = Md_D2 * D2 ...
    !if LongCobbDouglas
        !! gamma_Md*Pd2*D2 = mu_D2*Pmd*Md ...
    !else
        !! Md = gamma_Md * D2 ...
    !end 
    ;

    "Demand for local input in local sectors"
    D3 = D3_D2 * D2 ...
    !if LongCobbDouglas
        !! (1-gamma_Md)*Pd2*D2 = mu_D2*Pd3*D3 ...
    !else
        !! D3 = (1-gamma_Md) * D2 ...
    !end
    ;

    "Real nonprimary import share of stage T-2 output in local sectors"
    Md_D2 = ...
        + rho_Md_D2 * (Md_D2{-1}) ...
        + (1 - rho_Md_D2) * (&Md / &D2) ...
        + shk_Md_D2 ...
    !! Md_D2 = &Md / &D2;

    "Real domestic input share of stage T-2 output in local sectors"
    D3_D2 = ...
        + rho_D3_D2 * (D3_D2{-1}) ...
        + (1 - rho_D3_D2) * (&D3 / &D2) ...
        + shk_D3_D2 ...
    !! D3_D2 = &D3 / &D2;


    % Stage T-1 in local sectors: Primary inputs

    D1 = D2 / (1 - gamma_J);
    D1 = J / gamma_J;
    Pd1 = (1 - gamma_J)*Pd2 + gamma_J*Pj;


% __Productivity (Technology)__

    "Overall productivity"
    log(A/A{-1}) = ...
        + rho_A * log(A{-1}/A{-2}) ...
        + (1 - rho_A) * (log(ss_dA) + shk_A) ...
    !! A = ss_dA*A{-1};

    "Public infrastructure augmented productivity"
    AA = A + iota_kg*Kg;

    "Relative productivity in local sectors"
    log(Ad) = rho_Ad*log(Ad{-1}) + (1-rho_Ad)*(log(ss_Ad) + shk_Ad) ...
    !! Ad = ss_Ad;

    "Total productivity in local sectors"
    AAd = AA * Ad ...
    !! AAd = AA * ss_Ad;


% __Monetary policy__

    "Monetary policy reaction function"
    log(Rg) = ...
        + rho_Rg * log(Rg{-1}) ...
        + (1 - rho_Rg) * (log(&Rg) + React) ...
    !! dPc = ss_dPc;


    "Monetary policy reaction term"
    React = ...
        + kappa_dPc * log(dPc{+1}/ss_dPc) ...
        + kappa_dS * log(dS{+1}/&dS) ...
    !! React = 0;


% __World prices in local currency__

    Pmd = Pw;
    Pw = S * Pw_star;


% __Interest rate structure__

    Rg_star = Rw_star * exp( -zeta_Rg1*(NIP_NGDP - zeta_Rg0) );

    Rg/Rg_star = (S{+1}/S) * (dS/&dS)^(-lambda_S) ...
    !! Rg = Rg_star * dS;

    Rg_Rw = Rg_star / Rw_star;

    Rh = Rg * exp( zeta_Rh1*(Bhw_NGDP + zeta_Rh0) );


% __Growth rates__

    dPc = Pc/Pc{-1};
    dPcx = Pcx/Pcx{-1};
    dPi = Pi/Pi{-1};
    dNGDP = NGDP/NGDP{-1};
    dS = S/S{-1};


% __Reporting and calibration ratios__

    PiId_NGDP*NGDP = Pi*Id;
    PiId_PkdKd = Pi*Id / (Pkd*Kd);
    DLIopt_Copt = DLIopt / Copt;
    PcVh_NGDP*NGDP = Pc*Vh;
    PkdKd_NGDP*NGDP = Pkd*Kd;
    PkzKz_NGDP = Pkz*Kz / NGDP;
    PmdMd_NGDP*NGDP = Pmd*Md;

    "Profits of local producers to GDP"
    PIEd_NGDP = (Pd*D - Wd*Nd - Prkd*Kd{-1} - Pmd*Md - Pj*J) / NGDP;

    "Private residential investment to GDP"
    PiIh_NGDP = Pi*Ih / NGDP;

