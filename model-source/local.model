% Local economy


!variables

    "Public infrastructure auxiliary variable" AA
    Y1, Y2, Y3, Py3
    My_Y2, Y3_Y2
    "Monetary policy reaction term" React

    "Shadow Price of Optimizing Household Budget" Lambda
    "Total Private Consumption" Ch
    "Consumption of Optimizing Households" Copt
    "Private residential investment to GDP ratio" PiIh_NGDP

    "Consumption of Hand-to-Mouth Households" Chtm
    "Auxiliary Variable for Consumption to Net Worth Ratio" Copt_Vh_nu0
    "Disposable Labor Income of Optimizing Households" DLIopt
    % "Total disposable labor income to GDP ratio" PcDLI_NGDP
    "Household real net worth" Vh
    "Disposable labor income of hand-to-mouth households" DLIhtm
    "Hours worked in local sectors" Ny
    "Wage bill in local sectors to GDP ratio" WNy_NGDP
    "Physical investment in local sectors" Iy

    "Profit margins in local sectors" PSIy
    "Profits in local sectors to GDP ratio" PIEy_NGDP
    "Share of overhead labor in local sectors" N0y_Ny

    "Private residential and overhead capital" Kh
    "Private residential and overhead investment" Ih

    "Variable hours worked in local sectors" NNy
    "Overhead hours worked in local sectors" N0y
    "Imported inputs to local sectors" My
    "Production capital in local sectors" Ky
    "Local production of intermediate goods" Y
    "Overall technology" A
    "Relative productivity in local sectors" Ay
    "Total productivity in local sectors" AAy


    "Household net worth to GDP ratio" PcVh_NGDP
    "Capital in local sectors to GDP ratio" PkyKy_NGDP
    "Remittances from HTM households to GDP ratio" TFw_NGDP

    "Marginal cost of local intermediate goods" Py1
    "Markuped marginal cost in local sectors" Py
    "Price of Local Intermediate Goods without Commodity costs" Py2
    "Investment Goods Prices" Pid
    "Consumer Goods Prices" Pc
    "Consumer Goods Prices w/o VAT" Pcx
    "Price of Capital in Local Production" Pky
    "Rental Price of Capital in Local Production" Prky

    "Price of imported inputs in local sectors" Pmy
    "World prices in local currency" Pw
    "Nominal exchange rate" S
    "Nominal exchange rate, Y/Y" dS

    "Government local-currency debt rate" Rg
    "Government foreign-currency debt rate" Rg_star
    "Household shadow interest rate" Rh
    "Government-world spread" Rg_Rw

    "Gross Rate of Change in Consumer Prices" dPc
    "Gross Rate of Change in Consumer Prices w/o VAT" dPcx
    "Gross Rate of Change in Investment Prices" dPi
    "Gross Rate of Change in Nominal GDP" dNGDP

    "Private Investment in Local Production to GDP Ratio" PiIy_NGDP
    "Investment to Capital Ratio in Local Production" PiIy_PkyKy
    "Household NFA to GDP Ratio"  BWh_NGDP
    "Auxiliary Variable for Disposable Labor Income to Consumption Ratio" DLIopt_Copt

    % "Wage bill of HTM households to GDP ratio" WhtmNhtm_NGDP


!log-variables !all-but

    PiIh_NGDP
    Copt_Vh_nu0
    PiIy_NGDP
    PiIy_PkyKy
    BWh_NGDP
    PcVh_NGDP
    PkyKy_NGDP
    DLIopt_Copt
    TFw_NGDP
    My_Y2
    Y3_Y2
    PIEy_NGDP
    N0y_Ny
    PSIy
    React


!shocks

    "Shock to overall productivity" shk_A
    "Shock to relative sector productivity in local sectors" shk_Ay
    "Shock to exchange rate parity" shk_S
    shk_My_Y2
    shk_Y3_Y2


!parameters

    iota
    "Monetary policy reaction to inflation" kappa_dPc
    "Monetary policy reaction to depreciation" kappa_dS
    "A/R Monetary policy rate" rho_Rg
    "Imperfections in forex arbitrage" sigma

    rho_My_Y2
    rho_Y3_Y2

    "Share of expats in variable labor in local sectors" upsilon_Y

    % Steady-state parameters
    "Discount Factor" beta
    "Current Income Sensitivity of Intertemporally Optimizing Households" chi_dli
    "Sensitivity of utility to net worth" nu
    "Intercept in net worth utility component" nu0
    "Depreciation of production capital" delta_Ky
    "Depreciation of residential capital" delta_Kh
    "Markup in final goods sectors" mu_CI
    "Markup over variable factors in local sectors" mu_Y2
    "Import Share of local sectors" gamma_My
    "Labor Share of local sectors" gamma_Ny

    "Intercept in government spread" zeta_Rg0
    "Sensitivity of government spread to debt" zeta_Rg1
    "Intercept in household spread" zeta_Rh0
    "Sensitivity of household spread to debt" zeta_Rh1
    "Share of primary inputs used in local sectors" gamma_J;

    % Directly calibrated ratios and rates
    "S/S Remittances from HTM households to HTM wage bill" ss_TFw_WNhtm
    "S/S Private Residential (Housing) Investment to Investment in Local Production Ratio" ss_Kh_Ky
    "S/S Rate of Change in Overall Productivity" ss_dA
    "Inflation Target" ss_dPc
    "S/S Relative Sectoral Productivity in Local Production" ss_Ay
    "S/S Overhead Labor in Local Production" ss_N0y

    % Transitory Parameters
    "Habit in consumption" chi_c
    "Capital versus investment adjustment cost" phi_Y
    "Intensity of capital or investment adjustment cost" xi_Iy
    "Consumer Price Adjustment Cost" xi_Pc
    "Investment price adjustment cost" xi_Pi
    "Cost of hiring-firing variable labor in local sectors" xi_NNy
    "Autoregression in overall productivity" rho_A
    "Autoregression in relative sectoral productivity in local production" rho_Ay
    "Residential (housing) investment reaction to private investment to GDP ratio" lambda_Ih1


!substitutions

    % Auxiliary Constant to Make MUC equal 1/C in steady state
    chi_DLI0 := (1-chi_dli*&DLIopt_Copt-chi_c);

    % Investment adjustment cost term in local production
    xi_Iy0 := ((1-phi_Y)*log(ss_dA - (1-delta_Ky)) + phi_Y*log(ss_dA));
    AdjIy0 := xi_Iy*[ log(Iy)-(1-phi_Y)*log(Ky{-1})-phi_Y*log(Iy{-1}) - $xi_Iy0$ ];
    AdjIy1 := xi_Iy*[ log(Iy{+1})-(1-phi_Y)*log(Ky)-phi_Y*log(Iy) - $xi_Iy0$ ];

    % Consumer price adjustment cost term
    AdjPc0 := xi_Pc*[ log(dPcx) - log(dPcx{-1}) ];
    AdjPc1 := xi_Pc*[ log(dPcx{+1}) - log(dPcx) ];

    % Investment price adjustment cost term
    AdjPi0 := xi_Pi*[ log(dPi) - log(dPi{-1}) ];
    AdjPi1 := xi_Pi*[ log(dPi{+1}) - log(dPi) ];

    AdjNNy := xi_NNy*[ log(NNy/NNy{-1}) - beta*log(NNy{+1}/NNy) ];

    NNy_opt := (NNy * (1 - upsilon_Y));
    NNy_htm := (NNy * upsilon_Y);

    gamma_Y2 := (gamma_My^gamma_My * (1-gamma_My)^(1-gamma_My));


!equations

% __Optimizing households__

    "Intertemporal choice by optimizing households"
    Lambda = ...
        + beta*Lambda{+1}*Rh ...
        + nu*(Copt/Vh - nu0)/(Pc*Copt);

    "Demand for consumption by optimizing households"
    Lambda*Pc*(Copt - chi_dli*DLIopt - chi_c*ss_dA*Copt{-1}) = 1;

    "Net worth of optimizing households"
    Pc*Vh = Pky*Ky + (BWh_NGDP + BGh_NGDP)*NGDP;

    "Real current income of optimizing households"
    Pc*DLIopt = (WNopt_NGDP + TFopt_NGDP + TFq_NGDP)*NGDP;

    "Calibration of net worth utility component"
    Copt_Vh_nu0 = Copt/Vh - nu0;

    "Price of production capital for local sectors"
    Pky = Pid*(1 + $AdjIy0$ - beta*phi_Y*$AdjIy1$) ...
    !! Pky = Pid;

    "Creation of production capital for local sectors"
    Lambda*Pky = ...
        beta*Lambda{+1}*[ ...
            Pky{+1}*(1-delta_Ky) + Prky{+1} + Pky{+1}*(1-phi_Y)*&PiIy_PkyKy*ss_dA*$AdjIy1$ ...
        ] + nu*(Copt/Vh - nu0)*Pky/(Pc*Copt) ...
    !! Lambda*Pky = beta*Lambda{+1}*[ ...
            Pky{+1}*(1-delta_Ky) + Prky{+1} ...
        ] + nu*(Copt/Vh - nu0)*Pky/(Pc*Copt) ...
    ;

    "Stock-flow capital-investment for local sectors"
    Ky = (1-delta_Ky)*Ky{-1} + Iy;


% __Hand-to-mouth households__

    "Real current income of hand-to-mouth households"
    Pc*DLIhtm = (WNhtm_NGDP*(1 - TRexp) + TFhtm_NGDP - (TFw_NGDP + TXexp_NGDP))*NGDP;

    "Remittances from hand-to-mouth households"
    TFw_NGDP = ss_TFw_WNhtm * WNhtm_NGDP*(1 - TRexp);

    "Demand for consumption by optimizing households"
    Chtm = DLIhtm;


% __Total private consumption__

    "Total private consumption"
    Ch = Copt + Chtm;


% __Residential and overhead investment__

    "Residential and overhead investment"
    log(Ih) = log(ss_dA*Ih{-1}) + lambda_Ih1*log(Ky{+1}*ss_Kh_Ky/Kh{+1}) ...
    !! Kh = ss_Kh_Ky * Ky;

    "Residential and overhead capital"
    Kh = (1-delta_Kh)*Kh{-1} + Ih;


% __Local Distribution of Consumer and Investment Goods__

    % Staget T-0 in local sectors: Distribution
    Y = Y1;
    Py = mu_CI*Py1;

    % Final consumer price setting
    Py = Pcx*[1 + (mu_CI-1)*($AdjPc0$ - beta*$AdjPc1$)] ...
    !! Pcx = Py;

    % VAT applied on consumer goods
    Pc = Pcx * (1 + TRvat);

    % Final investment price setting
    Py = Pid*[1 + (mu_CI-1)*($AdjPi0$ - beta*$AdjPi1$)] ...
    !! Pid = Py;


% __Production for local markets__


    % Stage T-3 in local sectors: Domestic inputs

    "Breakdown of total labor into overhead and variable in local sectors"
    Ny = N0y + NNy;

    "Overhead labor in local sectors"
    N0y = ss_N0y;

    "Wage bill in local sectors"
    WNy_NGDP = Wy*Ny / NGDP;

    "Stage T-3 production technology in local sectors"
    Y3 = Ky{-1}^(1-gamma_Ny) * (A*Ay*NNy)^gamma_Ny;

    "Demand for labor in local sectors"
    gamma_Ny * Py3*Y3 = Wy * NNy * (1 + $AdjNNy$) ...
    !! gamma_Ny * Py3*Y3 = Wy * NNy;

    "Demand for capital in local sectors"
    (1-gamma_Ny)*Py3*Y3 = Prky*Ky{-1};


    % Stage T-2 in local sectors: Nonprimary import

    "Stage T-2 production technology in local sectors"
    Py2*Y2 = mu_Y2*(Pmy*My + Py3*Y3) ...
    !if LongCobbDouglas
        !! Y2 = (1/$gamma_Y2$) * My^gamma_My * Y3^(1-gamma_My) ...
    !else
        !! Py2*Y2 = mu_Y2*(Pmy*My + Py3*Y3) ...
    !end
    ;

    "Demand for nonprimary imports in local sectors"
    My = My_Y2 * Y2 ...
    !if LongCobbDouglas
        !! gamma_My*Py2*Y2 = mu_Y2*Pmy*My ...
    !else
        !! My = gamma_My * Y2 ...
    !end 
    ;

    "Demand for local input in local sectors"
    Y3 = Y3_Y2 * Y2 ...
    !if LongCobbDouglas
        !! (1-gamma_My)*Py2*Y2 = mu_Y2*Py3*Y3 ...
    !else
        !! Y3 = (1-gamma_My) * Y2 ...
    !end
    ;

    "Real nonprimary import share of stage T-2 output in local sectors"
    My_Y2 = ...
        + rho_My_Y2 * (My_Y2{-1}) ...
        + (1 - rho_My_Y2) * (&My / &Y2) ...
        + shk_My_Y2 ...
    !! My_Y2 = &My / &Y2;

    "Real domestic input share of stage T-2 output in local sectors"
    Y3_Y2 = ...
        + rho_Y3_Y2 * (Y3_Y2{-1}) ...
        + (1 - rho_Y3_Y2) * (&Y3 / &Y2) ...
        + shk_Y3_Y2 ...
    !! Y3_Y2 = &Y3 / &Y2;


    % Stage T-1 in local sectors: Primary inputs

    Y1 = Y2 / (1 - gamma_J);
    Y1 = J / gamma_J;
    Py1 = (1 - gamma_J)*Py2 + gamma_J*Pj;


% __Productivity (Technology)__

    "Overall productivity"
    log(A/A{-1}) = ...
        + rho_A * log(A{-1}/A{-2}) ...
        + (1 - rho_A) * (log(ss_dA) + shk_A) ...
    !! A = ss_dA*A{-1};

    "Public infrastructure augmented productivity"
    AA = A + iota*Kg;

    "Relative productivity in local sectors"
    log(Ay) = rho_Ay*log(Ay{-1}) + (1-rho_Ay)*(log(ss_Ay) + shk_Ay) ...
    !! Ay = ss_Ay;

    "Total productivity in local sectors"
    AAy = AA * Ay ...
    !! AAy = AA * ss_Ay;


% __Monetary policy__

    "Monetary policy reaction function"
    log(Rg) = ...
        + rho_Rg * log(Rg{-1}) ...
        + (1 - rho_Rg) * (log(&Rg) + React) ...
    !! dPc = ss_dPc;


    "Monetary policy reaction term"
    React = ...
        + kappa_dPc * log(dPc{+1}/ss_dPc) ...
        + kappa_dS * log(dS{+1}/&dS) ...
    !! React = 0;


% __World prices in local currency__

    Pmy = Pw;
    Pw = S * Pw_star;


% __Interest rate structure__

    Rg_star = Rw_star * exp( zeta_Rg1*(-NIP_NGDP_adj + zeta_Rg0) );

    Rg/Rg_star = (S{+1}/S) * (dS/&dS)^(-sigma) ...
    !! Rg = Rg_star * dS;

    Rg_Rw = Rg_star / Rw_star;

    Rh = Rg * exp( zeta_Rh1*(-BWh_NGDP + zeta_Rh0) );


% __Growth rates__

    dPc = Pc/Pc{-1};
    dPcx = Pcx/Pcx{-1};
    dPi = Pid/Pid{-1};
    dNGDP = NGDP/NGDP{-1};
    dS = S/S{-1};


% __Reporting and calibration ratios__

    PiIy_NGDP*NGDP = Pid*Iy;
    PiIy_PkyKy = Pid*Iy / (Pky*Ky);
    DLIopt_Copt = DLIopt / Copt;
    PcVh_NGDP*NGDP = Pc*Vh;
    PkyKy_NGDP*NGDP = Pky*Ky;

    "Profit margins (excluding adjustment costs, excluding input tax) in local production"
    PSIy * (Py*Y - PIEy_NGDP*NGDP) = Py*Y;

    "Profits of local producers to GDP ratio"
    PIEy_NGDP = (Py*Y - Wy*Ny - Prky*Ky{-1} - Pmy*My - Pj*J) / NGDP;

    "Share of overhead labor in local sectors"
    N0y_Ny = N0y / Ny;

    "Private residential investment to GDP ratio"
    PiIh_NGDP = Pid*Ih / NGDP;

